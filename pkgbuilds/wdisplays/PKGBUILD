# Maintainer: arti
# Maintainer: redtide <redtid3@gmail.com>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=wdisplays
pkgver=1.1.1
pkgrel=2
pkgdesc="GUI display configurator for wlroots compositors"
url="https://github.com/artizirk/wdisplays"
license=(GPL3)
arch=(x86_64)
depends=(
  gtk3
  libepoxy
  wayland
  wayland-protocols
)
makedepends=(
  meson
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
  outputs_noop.patch
)
sha512sums=('46923592e9fbb5c06c3549c198aedbafd8e0b5a6a82bc11d9d0b4cb0860070000cce18e0aa109932a4e4bee34d9f235660d2ea9d485a8fce146a2755ef62f81d'
            '75a8e46bf6ac33100aff32d41e82c3a250b607534315aed0f580508eb15620b329c69184300c9568dfe2f02e18485e1cc1777705892a924cfaecab0deee9f94f')
b2sums=('a294719c3b8e91642d53e22aaac40c3ddd9a24e2eaef8e8b412905e9a4ef4be7984c0d0c087661d975ec518304020d1f84fc7a6ac4f62f1a5708c73878646818'
        '422b4c1b7238ca2d57ba4d94123cea24079b60fcb5ada4df3e9404f01d34b1ab43061f5d9cd07be57d38a0b15ddac8f0e4bda72044a22e4cb52cc4e99e01a7e9')

prepare() {
  rm -rf build
  arch-meson "${pkgname}-${pkgver}" build
  patch --directory "${pkgname}-${pkgver}" --forward --strip=1 --input=../outputs_noop.patch
}

build() {
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir" ninja -C "$srcdir/build" install
}
