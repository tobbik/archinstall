# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname=wcm-git
pkgver=0.9.0.r4.g69a5aaf
pkgrel=2
pkgdesc='Wayfire Config Manager'
url=https://wayfire.org
arch=(x86_64 aarch64)
license=(custom:MIT)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
depends=(gtkmm3 wayfire-git wf-shell-git 'wdisplays>=1.1.1-2')
makedepends=(wayland-protocols meson ninja git extra-cmake-modules)
optdepends=("wayfire-plugins-extra: configuration for extra Wayfire plugins")
source=("${pkgname}::git+https://github.com/WayfireWM/wcm")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  ( set -o pipefail
    git describe --long --tag 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
  )
}

build () {
	rm -rf build
	sed -i "${pkgname}/meson_options.txt" -e "0,/enable_wdisplays/ s|true|false|"
	arch-meson "${pkgname}" build
	ninja -C build
}

package () {
	DESTDIR="${pkgdir}" ninja -C build install
}

